// ************************************************************
// 			  				   EXIT 
// ************************************************************

INSTANCE DIA_Richter_EXIT(C_INFO)
{
	npc			= Vlk_402_Richter;
	nr			= 999;
	condition	= DIA_Richter_EXIT_Condition;
	information	= DIA_Richter_EXIT_Info;
	permanent	= TRUE;
	description = DIALOG_ENDE;
};                       

FUNC INT DIA_Richter_EXIT_Condition()
{
	return TRUE;
};

FUNC VOID DIA_Richter_EXIT_Info()
{	
	AI_StopProcessInfos	(self);
};

// ************************************************************
// 			  				   Hello 
// ************************************************************

INSTANCE DIA_Richter_Hello(C_INFO)
{
	npc			= Vlk_402_Richter;
	nr			= 4;
	condition	= DIA_Richter_Hello_Condition;
	information	= DIA_Richter_Hello_Info;
	permanent	= FALSE;
	description = "Co tady dÏl·ö?";
};                       

FUNC INT DIA_Richter_Hello_Condition()
{
	if (MIS_Lee_JudgeRichter == 0)
		{
				return TRUE;
		};
};

FUNC VOID DIA_Richter_Hello_Info()
{	
	AI_Output (other,self ,"DIA_Richter_Hello_15_00"); //Co tady dÏl·ö?
	AI_Output (self ,other,"DIA_Richter_Hello_10_01"); //ProË mÏ obtÏûujeö? Kdo vlastnÏ jsi?
	AI_Output (other,self ,"DIA_Richter_Hello_15_02"); //J·, ehm...
	AI_Output (self ,other,"DIA_Richter_Hello_10_03"); //ÿekni, nezn·me se odnÏkud?
	
	Info_ClearChoices (DIA_Richter_Hello);
	Info_AddChoice (DIA_Richter_Hello,"Co si pamatuju, tak ne.",DIA_Richter_Hello_DontThinkSo);
	Info_AddChoice (DIA_Richter_Hello,"Moûn· ano.",DIA_Richter_Hello_MayBe);
};

FUNC VOID DIA_Richter_Hello_DontThinkSo ()
{
	AI_Output (other,self ,"DIA_Richter_Hello_DontThinkSo_15_00"); //Co si pamatuju, tak ne.
	AI_Output (self ,other,"DIA_Richter_Hello_DontThinkSo_10_01"); //To je zvl·ötnÌ. P¯Ìsahal bych, ûe uû jsme se nÏkde vidÏli.
	AI_Output (self ,other,"DIA_Richter_Hello_DontThinkSo_10_02"); //NevadÌ, vöak jsi jen uboh˝ öup·k. Vöichni vypad·te stejnÏ.
};

FUNC VOID DIA_Richter_Hello_MayBe ()
{
	AI_Output (other,self ,"DIA_Richter_Hello_MayBe_15_00"); //Moûn· ano.
	AI_Output (self ,other,"DIA_Richter_Hello_MayBe_10_01"); //ChtÏlo by to trochu uctivÏjöÌ tÛn! S k˝m si v˘bec myslÌö, ûe mluvÌö?
	AI_Output (self ,other,"DIA_Richter_Hello_MayBe_10_02"); //To nejmenöÌ, co m˘ûeö udÏlat, je mluvit se mnou odpovÌdajÌcÌm tÛnem.
	AI_Output (self ,other,"DIA_Richter_Hello_MayBe_10_03"); //A teÔ se mi ztraù z oËÌ!
	
	AI_StopProcessInfos (self);
};

// ************************************************************
// 			  				   Perm 
// ************************************************************

INSTANCE DIA_Richter_Perm(C_INFO)
{
	npc			= Vlk_402_Richter;
	nr			= 800;
	condition	= DIA_Richter_Perm_Condition;
	information	= DIA_Richter_Perm_Info;
	permanent	= TRUE;
	description = "Jak to jde?";
};                       

FUNC INT DIA_Richter_Perm_Condition()
{
	if ((Npc_KnowsInfo (other,DIA_Richter_Hello)) || (MIS_Lee_JudgeRichter == LOG_RUNNING))
	&& (SCIstRichtersLakai == FALSE)
	{
		return TRUE;
	};	
};

FUNC VOID DIA_Richter_Perm_Info()
{	
	AI_Output (other,self ,"DIA_Richter_Perm_15_00"); //Jak se vede?

	if (MIS_Lee_JudgeRichter == LOG_RUNNING)
	&& ((hero.guild == GIL_SLD) || (hero.guild == GIL_DJG ))
	{
		AI_Output (self ,other,"DIA_Richter_Perm_10_01"); //RadÏji bys mÏl vypadnout, ty mizernej ûold·ku, neû zavol·m str·ûe.
		AI_Output (other,self ,"DIA_Richter_Perm_15_02"); //KlÌdek. Nep¯iöel jsem tÏ okr·st. Hled·m pr·ci.
		AI_Output (self ,other,"DIA_Richter_Perm_10_03"); //¡. Tak ty pro mÏ chceö pracovat. Tak to je samoz¯ejmÏ jin·.
		AI_Output (self ,other,"DIA_Richter_Perm_10_04"); //Ale m· to jeden h·Ëek. NevÌm, jestli ti mohu vÏ¯it. Budeö mi muset nejprve prok·zat svou loajalitu.
		SCIstRichtersLakai = TRUE;
	}
	else
	{
		AI_Output (self ,other,"DIA_Richter_Perm_10_05"); //Ne¯ekl jsem snad jasnÏ, ûe s tebou nechci nic mÌt?
		AI_Output (other,self ,"DIA_Richter_Perm_15_06"); //Ne, ne tolika slovy.
		AI_Output (self ,other,"DIA_Richter_Perm_10_07"); //Tak douf·m, ûe ti to doölo teÔ.
	};
};

// ************************************************************
// 			  				   RichtersLakai 
// ************************************************************

INSTANCE DIA_Richter_RichtersLakai (C_INFO)
{
	npc			= Vlk_402_Richter;
	nr			= 2;
	condition	= DIA_Richter_RichtersLakai_Condition;
	information	= DIA_Richter_RichtersLakai_Info;

	description = "Otestuj si mÏ.";
};                       

FUNC INT DIA_Richter_RichtersLakai_Condition()
{
	if ((Npc_KnowsInfo (other,DIA_Richter_Hello)) || (MIS_Lee_JudgeRichter != 0))
	&& (SCIstRichtersLakai == TRUE)
	{
		return TRUE;
	};	
};

FUNC VOID DIA_Richter_RichtersLakai_Info()
{	
	AI_Output (other,self ,"DIA_Richter_RichtersLakai_15_00"); //Otestuj si mÏ.
	AI_Output (self ,other,"DIA_Richter_RichtersLakai_10_01"); //Mmh. Dobr·. Poslouchej. P¯ines mi od ohniv˝ch m·g˘ posv·tnÈ kladivo. Ukr˝vajÌ ho nÏkde v podzemÌ svÈho kl·ötera.
	AI_Output (self ,other,"DIA_Richter_RichtersLakai_10_02"); //Jestli to dok·ûeö splnit, moûn· budu uvaûovat o tom, ûe bych si tÏ najal jako svÈho osobnÌho str·ûce.
	
	Info_ClearChoices	(DIA_Richter_RichtersLakai);
	Info_AddChoice	(DIA_Richter_RichtersLakai, "To m·m jako okr·st m·gy? Nejsi trochu mimo?", DIA_Richter_RichtersLakai_nein );
	Info_AddChoice	(DIA_Richter_RichtersLakai, "Dobr·.", DIA_Richter_RichtersLakai_ja );
	
	B_LogEntry (TOPIC_RichterLakai,"Soudce mi nevÏ¯Ì. MusÌm mu prok·zat svou loajalitu - ukr·st ze sklepenÌ kl·ötera ohniv˝ch m·g˘ posv·tnÈ kladivo a p¯inÈst mu je."); 

	MIS_Richter_BringHolyHammer = LOG_RUNNING;
};
func void DIA_Richter_RichtersLakai_nein ()
{
	AI_Output			(other, self, "DIA_Richter_RichtersLakai_nein_15_00"); //To m·m jako okr·st m·gy? Nejsi trochu mimo?
	AI_Output			(self, other, "DIA_Richter_RichtersLakai_nein_10_01"); //V tom p¯ÌpadÏ t·hni do pekel a p¯estaÚ pl˝tvat m˝m Ëasem.
	AI_StopProcessInfos (self);
};

func void DIA_Richter_RichtersLakai_ja ()
{
	AI_Output			(other, self, "DIA_Richter_RichtersLakai_ja_15_00"); //Dobr·.
	AI_Output			(self, other, "DIA_Richter_RichtersLakai_ja_10_01"); //Fajn. Ale pamatuj - jestli tÏ chytÌ, nikdy jsem o tobÏ neslyöel.
	AI_StopProcessInfos (self);
};


// ************************************************************
// 			  				   KillMorgahard 
// ************************************************************

INSTANCE DIA_Richter_KillMorgahard (C_INFO)
{
	npc			= Vlk_402_Richter;
	nr			= 2;
	condition	= DIA_Richter_KillMorgahard_Condition;
	information	= DIA_Richter_KillMorgahard_Info;

	description =	"Tady je posv·tnÈ kladivo m·g˘.";
};                       

FUNC INT DIA_Richter_KillMorgahard_Condition()
{
	if (Npc_HasItems (other,Holy_Hammer_MIS))
	&& (MIS_Richter_BringHolyHammer == LOG_RUNNING)
	{
		return TRUE;
	};	
};

FUNC VOID DIA_Richter_KillMorgahard_Info()
{	
	AI_Output (other,self ,"DIA_Richter_KillMorgahard_15_00"); //Tady je posv·tnÈ kladivo m·g˘.
	B_GiveInvItems (other, self, Holy_Hammer_MIS,1);
	AI_Output (self ,other,"DIA_Richter_KillMorgahard_10_01"); //Jsem opravdu p¯ekvapen˝. Ty to myslÌö v·ûnÏ, co? Tady je tv· odmÏna.
	CreateInvItems (self, ItMi_Gold, 300);									
	B_GiveInvItems (self, other, ItMi_Gold, 300);					
	AI_Output (self ,other,"DIA_Richter_KillMorgahard_10_02"); //Fajn. Kdyû se to m· takhle, tak to m·m pro tebe jeötÏ dalöÌ ˙kol.
	AI_Output (self ,other,"DIA_Richter_KillMorgahard_10_03"); //P¯ed nÏkolika dny uteklo ze zdejöÌho vÏzenÌ p·r trestanc˘.
	AI_Output (self ,other,"DIA_Richter_KillMorgahard_10_04"); //Ti zkurvysyni se schovali v hor·ch. NÏkde smÏrem ke XardasovÏ novÈ vÏûi.
	AI_Output (self ,other,"DIA_Richter_KillMorgahard_10_05"); //Jejich v˘dce se jmenuje Morgahard. Chci, abys ho zabil.
	AI_Output (self ,other,"DIA_Richter_KillMorgahard_10_06"); //PospÏö si, neû n·m proklouzne.
	
	Wld_InsertNpc	(BDT_1030_Morgahard,"REICH"); 
	Wld_InsertNpc	(BDT_1031_Fluechtling,"REICH"); 
	Wld_InsertNpc	(BDT_1032_Fluechtling,"REICH"); 
	Wld_InsertNpc	(BDT_1033_Fluechtling,"REICH"); 
	Wld_InsertNpc	(BDT_1034_Fluechtling,"REICH"); 
	Wld_InsertNpc	(BDT_1035_Fluechtling,"REICH"); 

	B_InitNpcGlobals ();

	B_GivePlayerXP (XP_BringHolyHammer);
	MIS_Richter_BringHolyHammer = LOG_SUCCESS;
	B_LogEntry (TOPIC_RichterLakai,"NÏkte¯Ì vÏzni unikli spravedlnosti. J· je m·m dostihnout a zabÌt jejich velitele Morgaharda. NejspÌö se ukr˝vajÌ v hor·ch, kdesi smÏrem ke XardasovÏ novÈ vÏûi."); 
	MIS_Richter_KillMorgahard = LOG_RUNNING;
	AI_StopProcessInfos (self);
};	

// ************************************************************
// 	  KilledMorgahard 
// ************************************************************

INSTANCE DIA_Richter_KilledMorgahard (C_INFO)
{
	npc			= Vlk_402_Richter;
	nr			= 2;
	condition	= DIA_Richter_KilledMorgahard_Condition;
	information	= DIA_Richter_KilledMorgahard_Info;

	description =	"Morgahard je mrtv˝.";
};                       

FUNC INT DIA_Richter_KilledMorgahard_Condition()
{
	if (Npc_IsDead(Morgahard))
	&& (MIS_Richter_KillMorgahard == LOG_RUNNING)
	{
		return TRUE;
	};	
};

FUNC VOID DIA_Richter_KilledMorgahard_Info()
{	
	AI_Output (other,self ,"DIA_Richter_KilledMorgahard_15_00"); //Morgahard je mrtv˝.
	AI_Output (self ,other,"DIA_Richter_KilledMorgahard_10_01"); //¡. Jsi dobrej chlap. Tady je tvoje odmÏna.
	CreateInvItems (self, ItMi_Gold, 400);									
	B_GiveInvItems (self, other, ItMi_Gold, 400);					
	B_GivePlayerXP (XP_KillMorgahard);
	B_LogEntry (TOPIC_RichterLakai,"Soudce byl zpr·vou o MorgahardovÏ smrti oËividnÏ nadöen. No, nechme ho b˝t. NesmÌm odboËovat od posl·nÌ, kter˝m mÏ povÏ¯il Lee."); 
	MIS_Richter_KillMorgahard = LOG_SUCCESS;
};

// ************************************************************
// 	  KilledMorgahardPERM 
// ************************************************************

INSTANCE DIA_Richter_KilledMorgahardPERM (C_INFO)
{
	npc			= Vlk_402_Richter;
	nr			= 2;
	condition	= DIA_Richter_KilledMorgahardPERM_Condition;
	information	= DIA_Richter_KilledMorgahardPERM_Info;
	permanent	 = 	TRUE;

	description =	"M˘ûu pro tebe jeötÏ nÏco udÏlat?";
};                       

FUNC INT DIA_Richter_KilledMorgahardPERM_Condition()
{
	if (MIS_Richter_KillMorgahard == LOG_SUCCESS)
	&& (MIS_RichtersPermissionForShip == 0)
	{
		return TRUE;
	};	
};

FUNC VOID DIA_Richter_KilledMorgahardPERM_Info ()
{	
	AI_Output (other,self ,"DIA_Richter_KilledMorgahardPERM_15_00"); //M˘ûu pro tebe jeötÏ nÏco udÏlat?
	AI_Output (self ,other,"DIA_Richter_KilledMorgahardPERM_10_01"); //TeÔ zrovna ne. Moûn· pozdÏji.
};

// ************************************************************
// 		 PermissionForShip 
// ************************************************************

INSTANCE DIA_Richter_PermissionForShip (C_INFO)
{
	npc			= Vlk_402_Richter;
	nr			= 2;
	condition	= DIA_Richter_PermissionForShip_Condition;
	information	= DIA_Richter_PermissionForShip_Info;

	description =	"Dej mi pÌsemnÈ povÏ¯enÌ pro paladinskou loÔ.";
};                       

FUNC INT DIA_Richter_PermissionForShip_Condition()
{
	if (MIS_RichtersPermissionForShip == LOG_RUNNING)
	{
		return TRUE;
	};	
};

FUNC VOID DIA_Richter_PermissionForShip_Info ()
{	
	AI_Output (other,self ,"DIA_Richter_PermissionForShip_15_00"); //Dej mi pÌsemnÈ povÏ¯enÌ pro paladinskou loÔ.
	AI_Output (self ,other,"DIA_Richter_PermissionForShip_10_01"); //(smÌch) Ty ses musel zbl·znit. Co to po mnÏ chceö?
	AI_Output (other,self ,"DIA_Richter_PermissionForShip_15_02"); //Slyöel jsi dob¯e. Dej mi to pÌsemnÈ opr·vnÏnÌ, nebo tÏ pr·sknu domobranÏ.
	AI_Output (self ,other,"DIA_Richter_PermissionForShip_10_03"); //(vrËÌ) Nenech·m se vydÌrat! Ne od tebe! Nech·m tÏ seûrat vlk˘m, ty mizernej ömejde.
	AI_Output (other,self ,"DIA_Richter_PermissionForShip_15_04"); //Morgahard napadl mÌstodrûÌcÌho na tv˘j rozkaz. M·m dokument, kter˝ to dokazuje.
	AI_Output (self ,other,"DIA_Richter_PermissionForShip_10_05"); //(vrËÌ) Arrh.
	AI_Output (self ,other,"DIA_Richter_PermissionForShip_10_06"); //Toho budeö litovat. Nikdo mÏ nebude vydÌrat, aniû by z toho vyv·znul se zdravou k˘ûÌ.
	AI_Output (self ,other,"DIA_Richter_PermissionForShip_10_07"); //Tady je to opr·vnÏnÌ. A teÔ vypadni. Vy¯ÌdÌm si to s tebou pozdÏji.
	CreateInvItems (self,ItWr_ForgedShipLetter_Mis,1);
	B_GiveInvItems (self,other,ItWr_ForgedShipLetter_Mis,1);
	B_LogEntry (TOPIC_Ship,"S plnou mocÌ v kapse mohu zÌskat loÔ, neû bys ¯ekl övec. Jsem zvÏdav˝, co tomu ¯ekne Lee."); 
	MIS_RichtersPermissionForShip = LOG_SUCCESS;
	B_GivePlayerXP (XP_RichtersPermissionForShip);
};

// ************************************************************
// 		 perm2 
// ************************************************************

INSTANCE DIA_Richter_perm2 (C_INFO)
{
	npc			= Vlk_402_Richter;
	nr			= 2;
	condition	= DIA_Richter_perm2_Condition;
	information	= DIA_Richter_perm2_Info;
	important	 = 	TRUE;
	permanent	 = 	TRUE;

};                       

FUNC INT DIA_Richter_perm2_Condition()
{
	if (MIS_RichtersPermissionForShip == LOG_SUCCESS)
	&& (Npc_IsInState (self,ZS_Talk))
		{
			return TRUE;
		};	
};

FUNC VOID DIA_Richter_perm2_Info ()
{	
	AI_Output (self ,other,"DIA_Richter_perm2_10_00"); //Ztraù se mi z oËÌ.
	AI_StopProcessInfos (self);
};


// ************************************************************
// 			  				PICK POCKET
// ************************************************************

INSTANCE DIA_Richter_PICKPOCKET (C_INFO)
{
	npc			= Vlk_402_Richter;
	nr			= 900;
	condition	= DIA_Richter_PICKPOCKET_Condition;
	information	= DIA_Richter_PICKPOCKET_Info;
	permanent	= TRUE;
	description = "(Tento klÌË p˘jde ukr·st snadno.)";
};                       

FUNC INT DIA_Richter_PICKPOCKET_Condition()
{
	if (Npc_GetTalentSkill (other,NPC_TALENT_PICKPOCKET) == 1) 
	&& (self.aivar[AIV_PlayerHasPickedMyPocket] == FALSE)
	&& (Npc_HasItems(self, ItKe_Richter) >= 1)
	&& (other.attribute[ATR_DEXTERITY] >= (30 - Theftdiff))
	{
		return TRUE;
	};
};
 
FUNC VOID DIA_Richter_PICKPOCKET_Info()
{	
	Info_ClearChoices	(DIA_Richter_PICKPOCKET);
	Info_AddChoice		(DIA_Richter_PICKPOCKET, DIALOG_BACK 		,DIA_Richter_PICKPOCKET_BACK);
	Info_AddChoice		(DIA_Richter_PICKPOCKET, DIALOG_PICKPOCKET	,DIA_Richter_PICKPOCKET_DoIt);
};

func void DIA_Richter_PICKPOCKET_DoIt()
{
	if (other.attribute[ATR_DEXTERITY] >= 30)
	{
		B_GiveInvItems (self, other, ItKe_Richter, 1);
		self.aivar[AIV_PlayerHasPickedMyPocket] = TRUE;
		B_GiveThiefXP ();	
		Info_ClearChoices (DIA_Richter_PICKPOCKET);
	}
	else
	{
		B_ResetThiefLevel();
		AI_StopProcessInfos	(self);
		B_Attack (self, other, AR_Theft, 1); 
	};
};
	
func void DIA_Richter_PICKPOCKET_BACK()
{
	Info_ClearChoices (DIA_Richter_PICKPOCKET);
};




