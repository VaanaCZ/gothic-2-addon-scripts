// ************************************************************
// 			  				   EXIT 
// ************************************************************
INSTANCE DIA_Thorben_EXIT(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 999;
	condition	= DIA_Thorben_EXIT_Condition;
	information	= DIA_Thorben_EXIT_Info;
	permanent	= TRUE;
	description = DIALOG_ENDE;
};                       
FUNC INT DIA_Thorben_EXIT_Condition()
{
		return TRUE;
};
FUNC VOID DIA_Thorben_EXIT_Info()
{	
	AI_StopProcessInfos	(self);
};

// ************************************************************
// 			  				PICK POCKET
// ************************************************************

INSTANCE DIA_Thorben_PICKPOCKET (C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 900;
	condition	= DIA_Thorben_PICKPOCKET_Condition;
	information	= DIA_Thorben_PICKPOCKET_Info;
	permanent	= TRUE;
	description = Pickpocket_40;
};                       

FUNC INT DIA_Thorben_PICKPOCKET_Condition()
{
	C_Beklauen (30, 28);
};
 
FUNC VOID DIA_Thorben_PICKPOCKET_Info()
{	
	Info_ClearChoices	(DIA_Thorben_PICKPOCKET);
	Info_AddChoice		(DIA_Thorben_PICKPOCKET, DIALOG_BACK 		,DIA_Thorben_PICKPOCKET_BACK);
	Info_AddChoice		(DIA_Thorben_PICKPOCKET, DIALOG_PICKPOCKET	,DIA_Thorben_PICKPOCKET_DoIt);
};

func void DIA_Thorben_PICKPOCKET_DoIt()
{
	B_Beklauen ();
	Info_ClearChoices (DIA_Thorben_PICKPOCKET);
};
	
func void DIA_Thorben_PICKPOCKET_BACK()
{
	Info_ClearChoices (DIA_Thorben_PICKPOCKET);
};

// ************************************************************
// 		NEWS - Gritta ist tot Thorben ist angepisst
// ************************************************************
INSTANCE DIA_Thorben_angepisst(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 1;
	condition	= DIA_Thorben_angepisst_Condition;
	information	= DIA_Thorben_angepisst_Info;
	permanent	= TRUE;
	important   = TRUE; 
};                       
FUNC INT DIA_Thorben_angepisst_Condition()
{	
	if (Npc_IsDead (Gritta))
	&& (Npc_IsInState (self, ZS_Talk))
	{	
		return TRUE;
	};
};
FUNC VOID DIA_Thorben_angepisst_Info()
{	
	AI_Output (self, other,"DIA_Thorben_angepisst_06_00"); //Zavraûdil jsi moji Grittu. To ti nikdy neodpustÌm. BÏû mi z oËÌ, vrahu!
	AI_StopProcessInfos (self);
};

// ************************************************************
// 		Hallo
// ************************************************************
INSTANCE DIA_Thorben_Hallo(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 2;
	condition	= DIA_Thorben_Hallo_Condition;
	information	= DIA_Thorben_Hallo_Info;
	permanent	= FALSE;
	important   = TRUE;
};                       
FUNC INT DIA_Thorben_Hallo_Condition()
{	
	if (Npc_IsInState (self, ZS_Talk))
	&& (self.aivar[AIV_TalkedToPlayer] == FALSE)
	&& (other.guild == GIL_NONE)
	{
		return TRUE;
	};
};
FUNC VOID DIA_Thorben_Hallo_Info()
{	
	AI_Output (self, other,"DIA_Thorben_Hallo_06_00"); //¡! Nov· tv·¯. Ty nejsi z Khorinidu, co?
	AI_Output (self, other,"DIA_Thorben_Hallo_06_01"); //Tohle nenÌ ta spr·vn· doba pro poutnÌky. Vöude samÌ banditi, û·dn· pr·ce a teÔ si jeötÏ zaËali vyskakovat rolnÌci.
	AI_Output (self, other,"DIA_Thorben_Hallo_06_02"); //Co tÏ sem p¯iv·dÌ?
};

// ************************************************************
// 		Suche Arbeit			//E1
// ************************************************************
INSTANCE DIA_Thorben_Arbeit(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 2;
	condition	= DIA_Thorben_Arbeit_Condition;
	information	= DIA_Thorben_Arbeit_Info;
	permanent	= FALSE;
	description = "Hled·m pr·ci.";
};                       
FUNC INT DIA_Thorben_Arbeit_Condition()
{	
	return TRUE;
};
FUNC VOID DIA_Thorben_Arbeit_Info()
{	
	AI_Output (other, self,"DIA_Thorben_Arbeit_15_00"); //Hled·m pr·ci.
	AI_Output (self, other,"DIA_Thorben_Arbeit_06_01"); //VÌö nÏco o truhla¯inÏ?
	AI_Output (other, self,"DIA_Thorben_Arbeit_15_02"); //Jedin˝, co dok·ûu ze d¯eva udÏlat, je oheÚ.
	AI_Output (self, other,"DIA_Thorben_Arbeit_06_03"); //A co z·mky?
	AI_Output (other, self,"DIA_Thorben_Arbeit_15_04"); //Noooo...
	AI_Output (self, other,"DIA_Thorben_Arbeit_06_05"); //Je mi lÌto, ale pokud o mÈm ¯emesle nic nevÌö, nem˘ûu tÏ pot¯ebovat.
	AI_Output (self, other,"DIA_Thorben_Arbeit_06_06"); //A nem·m penÌze na to, abych si platil uËednÌka.
	
	Log_CreateTopic (TOPIC_Lehrling,LOG_MISSION); 
	Log_SetTopicStatus (TOPIC_Lehrling,LOG_RUNNING);
	B_LogEntry (TOPIC_Lehrling, "Thorben mÏ do uËenÌ nep¯ijme.");
};

// ************************************************************
// 		Bei anderem Meister		(MIS Blessings)	//E2
// ************************************************************
INSTANCE DIA_Thorben_OtherMasters(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 2;
	condition	= DIA_Thorben_OtherMasters_Condition;
	information	= DIA_Thorben_OtherMasters_Info;
	permanent	= FALSE;
	description = "Co kdybych chtÏl zaËÌt jako uËednÌk u jednoho z ostatnÌch mistr˘?";
};                       
FUNC INT DIA_Thorben_OtherMasters_Condition()
{	
	if (Npc_KnowsInfo (other, DIA_Thorben_Arbeit))
	&& (Player_IsApprentice == APP_NONE)
	{
		return TRUE;
	};
};
FUNC VOID DIA_Thorben_OtherMasters_Info()
{	
	AI_Output (other, self,"DIA_Thorben_OtherMasters_15_00"); //Co kdybych chtÏl zaËÌt jako uËednÌk u jednoho z ostatnÌch mistr˘?
	AI_Output (self, other,"DIA_Thorben_OtherMasters_06_01"); //Dobr·, d·m ti svÈ doporuËenÌ.
	AI_Output (self, other,"DIA_Thorben_OtherMasters_06_02"); //Ale nejd¯Ìv bys mÏl zÌskat poûehn·nÌ od boh˘.
	AI_Output (self, other,"DIA_Thorben_OtherMasters_06_03"); //ÿekni, jsi zboûn˝ muû?
	
	Info_ClearChoices (DIA_Thorben_OtherMasters);
	if (other.guild != GIL_KDF)
	&& (other.guild != GIL_NOV)
	&& (other.guild != GIL_PAL)
	{
		Info_AddChoice (DIA_Thorben_OtherMasters, "No, kdyû myslÌö, ûe bych se mÏl modlit pravidelnÏ...", DIA_Thorben_OtherMasters_Naja);
	};
	Info_AddChoice (DIA_Thorben_OtherMasters, "Ano. NejpokornÏjöÌ sluûebnÌk, mist¯e Thorbne.", DIA_Thorben_OtherMasters_Devoutly);
};

func void B_Thorben_GetBlessings()
{
	AI_Output (self, other,"B_Thorben_GetBlessings_06_00"); //Tak to jdi za Vatrasem, Adanov˝m knÏzem, a popros ho, aù ti poûehn·.
	if (other.guild != GIL_KDF)
	&& (other.guild != GIL_NOV)
	&& (other.guild != GIL_PAL)
	{
		AI_Output (self, other,"B_Thorben_GetBlessings_06_01"); //On uû ti ¯ekne, kde najÌt Innosovy knÏze. Taky od nÏj m˘ûeö dostat poûehn·nÌ.
	};
	AI_Output (self, other,"B_Thorben_GetBlessings_06_02"); //Jakmile zÌsk·ö poûehn·nÌ, m·ö m˘j souhlas.
	
	MIS_Thorben_GetBlessings = LOG_RUNNING;
	
	Log_CreateTopic(TOPIC_Thorben,LOG_MISSION);
	Log_SetTopicStatus(TOPIC_Thorben,LOG_RUNNING);
	B_LogEntry (TOPIC_Thorben,"Thorben se za mÏ p¯imluvÌ teprve tehdy, aû si vyprosÌm poûehn·nÌ od Adanova a Innosova knÏze.");
};

func void DIA_Thorben_OtherMasters_Devoutly()
{
	AI_Output (other, self,"DIA_Thorben_OtherMasters_Devoutly_15_00"); //Ano. NejpokornÏjöÌ sluûebnÌk, mist¯e Thorbene.
	B_Thorben_GetBlessings();
	
	Info_ClearChoices (DIA_Thorben_OtherMasters);
};

func void DIA_Thorben_OtherMasters_Naja()
{
	AI_Output (other, self,"DIA_Thorben_OtherMasters_Naja_15_00"); //No, kdyû myslÌö, ûe bych se mÏl modlit pravidelnÏ...
	AI_Output (self, other,"DIA_Thorben_OtherMasters_Naja_06_01"); //J· si to nemyslÌm. VÌm to!
	AI_Output (self, other,"DIA_Thorben_OtherMasters_Naja_06_02"); //NÏkdo, kdo nenÌ dostateËnÏ zboûn˝, m˘j souhlas nikdy nezÌsk·.
	AI_Output (self, other,"DIA_Thorben_OtherMasters_Naja_06_03"); //Pros bohy za shovÌvavost nad tv˝mi h¯Ìchy.
	B_Thorben_GetBlessings();
	
	Info_ClearChoices (DIA_Thorben_OtherMasters);
};

// ************************************************************
// 		ZUSTIMMUNG		//E3  (PERM)
// ************************************************************
INSTANCE DIA_Thorben_ZUSTIMMUNG(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 1;
	condition	= DIA_Thorben_ZUSTIMMUNG_Condition;
	information	= DIA_Thorben_ZUSTIMMUNG_Info;
	permanent	= TRUE;
	description = "Tak jak s tÌm doporuËenÌm, mist¯e?";
};                       
FUNC INT DIA_Thorben_ZUSTIMMUNG_Condition()
{	
	if (Player_IsApprentice == APP_NONE)
	&& (MIS_Thorben_GetBlessings == LOG_RUNNING)
	{
		return TRUE;
	};
};
FUNC VOID DIA_Thorben_ZUSTIMMUNG_Info()
{	
	AI_Output (other, self,"DIA_Thorben_ZUSTIMMUNG_15_00"); //Tak jak s tÌm doporuËenÌm, mist¯e?
	AI_Output (self, other,"DIA_Thorben_ZUSTIMMUNG_06_01"); //Poûehnal ti Vatras?
	
	if (Vatras_Segen == TRUE)
	{
		AI_Output (other, self,"DIA_Thorben_ZUSTIMMUNG_15_02"); //Ano.
		AI_Output (self, other,"DIA_Thorben_ZUSTIMMUNG_06_03"); //A zÌskal jsi takÈ poûehn·nÌ Innose?
		
		if (Daron_Segen == TRUE)
		|| (Isgaroth_Segen == TRUE)
		|| (other.guild == GIL_KDF)
		{
			AI_Output (other, self,"DIA_Thorben_ZUSTIMMUNG_15_04"); //Ano, zÌskal.
			AI_Output (self, other,"DIA_Thorben_ZUSTIMMUNG_06_05"); //V tom p¯ÌpadÏ m·ö i mÈ poûehn·nÌ. Nez·leûÌ na tom, jakou cestu sis vybral - buÔ hrd˝ na svou pr·ci, chlapËe!
			
			MIS_Thorben_GetBlessings = LOG_SUCCESS;
			B_GivePlayerXP (XP_Zustimmung);
			
			Log_CreateTopic (TOPIC_Lehrling,LOG_MISSION);
			Log_SetTopicStatus (TOPIC_Lehrling,LOG_RUNNING);
			B_LogEntry (TOPIC_Lehrling,"Budu-li k nÏkomu chtÌt vstoupit do uËenÌ, Thorben se za mÏ p¯imluvÌ.");
		}
		else
		{
			AI_Output (other, self,"DIA_Thorben_ZUSTIMMUNG_15_06"); //Ne. JeötÏ ne.
			AI_Output (self, other,"DIA_Thorben_ZUSTIMMUNG_06_07"); //Zn·ö moje podmÌnky. Sv˝m z·leûitostem se m˘ûeö vÏnovat jen s poûehn·nÌm boh˘.
		};
	}
	else
	{
		AI_Output (other, self,"DIA_Thorben_ZUSTIMMUNG_15_08"); //JeötÏ ne...
		AI_Output (self, other,"DIA_Thorben_ZUSTIMMUNG_06_09"); //V tom p¯ÌpadÏ nevÌm, proË se mÏ znovu pt·ö. Zn·ö moje podmÌnky.
	};
};

// ************************************************************
// 		Was weiﬂt du ¸ber Schlˆsser?		//E2
// ************************************************************
INSTANCE DIA_Thorben_Locksmith(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 2;
	condition	= DIA_Thorben_Locksmith_Condition;
	information	= DIA_Thorben_Locksmith_Info;
	permanent	= FALSE;
	description = "Tak ty se vyzn·ö v z·mcÌch?";
};                       
FUNC INT DIA_Thorben_Locksmith_Condition()
{	
	if (Npc_KnowsInfo (other, DIA_Thorben_Arbeit))
	{
		return TRUE;
	};
};
FUNC VOID DIA_Thorben_Locksmith_Info()
{	
	AI_Output (other, self,"DIA_Thorben_Locksmith_15_00"); //Tak ty se vyzn·ö v z·mcÌch?
	AI_Output (self, other,"DIA_Thorben_Locksmith_06_01"); //Co by to bylo za po¯·dnou truhlu bez dobrÈho z·mku?
	AI_Output (self, other,"DIA_Thorben_Locksmith_06_02"); //Vyr·bÌm si svÈ vlastnÌ z·mky. Takhle si m˘ûu b˝t p¯inejmenöÌm jist˝, ûe jsem neudÏlal bytelnou truhlu jen tak pro nic za nic.
	AI_Output (self, other,"DIA_Thorben_Locksmith_06_03"); //Odbyt˝ z·mek se d· snadno zlomit. A tady v Khorinidu se vöude kolem potuluje spousta zlodÏj˘. Zvl·ötÏ poslednÌ dobou!
};

// ************************************************************
// 		Schuldenbuch
// ************************************************************
INSTANCE DIA_Thorben_Schuldenbuch(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 2;
	condition	= DIA_Thorben_Schuldenbuch_Condition;
	information	= DIA_Thorben_Schuldenbuch_Info;
	permanent	= FALSE;
	description = "M·m tady Lehmarovu ˙ËetnÌ knihu...";
};                       
FUNC INT DIA_Thorben_Schuldenbuch_Condition()
{	
	if (Npc_HasItems (other, ItWr_Schuldenbuch) > 0)
	{
		return TRUE;
	};
};
FUNC VOID DIA_Thorben_Schuldenbuch_Info()
{	
	AI_Output (other, self,"DIA_Thorben_Schuldenbuch_15_00"); //M·m tady Lehmarovu ˙ËetnÌ knihu.
	AI_Output (self, other,"DIA_Thorben_Schuldenbuch_06_01"); //(podezÌravÏ) Jak ses k tomu dostal?
	AI_Output (other, self,"DIA_Thorben_Schuldenbuch_15_02"); //SpÌö by tÏ mÏlo zajÌmat, ûe je v nÌ tvÈ jmÈno.
	AI_Output (self, other,"DIA_Thorben_Schuldenbuch_06_03"); //Dej to sem!
	B_GiveInvItems (other, self, ItWr_Schuldenbuch, 1);
	AI_Output (other, self,"DIA_Thorben_Schuldenbuch_15_04"); //A co mi za to d·ö?
	AI_Output (self, other,"DIA_Thorben_Schuldenbuch_06_05"); //Nem·m penÌze nazbyt a nem˘ûu ti d·t nic jinÈho neû mÈ srdeËnÈ podÏkov·nÌ.
	B_GivePlayerXP (XP_Schuldenbuch);
};

// ************************************************************
// 		Kann ich Schlˆsser knacken lernen		//E3
// ************************************************************
INSTANCE DIA_Thorben_PleaseTeach(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 2;
	condition	= DIA_Thorben_PleaseTeach_Condition;
	information	= DIA_Thorben_PleaseTeach_Info;
	permanent	= TRUE;
	description = "M˘ûeö mÏ nauËit, jak p·Ëit z·mky?";
};                       
FUNC INT DIA_Thorben_PleaseTeach_Condition()
{	
	if (Npc_KnowsInfo (other, DIA_Thorben_Locksmith))
	&& (Thorben_TeachPlayer == FALSE)
	&& (Npc_GetTalentSkill (other, NPC_TALENT_PICKLOCK) == 0)
	{
		return TRUE;
	};
};
FUNC VOID DIA_Thorben_PleaseTeach_Info()
{	
	AI_Output (other, self,"DIA_Thorben_PleaseTeach_15_00"); //M˘ûeö mÏ nauËit, jak p·Ëit z·mky?
		
	if (Npc_HasItems (self, ItWr_Schuldenbuch) > 0)
	{
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_06_01"); //Kdyby nebylo tebe, budu Lehmarovi platit po cel˝ zbytek svÈho ûivota.
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_06_02"); //NauËÌm tÏ, co budeö chtÌt.
		Thorben_TeachPlayer = TRUE;
	}
	else if (Thorben_GotGold == TRUE) //100 Gold bekommen
	{
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_06_03"); //P¯inesl jsi mi 100 zlat˝ch. To od tebe bylo velmi milÈ.
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_06_04"); //P¯iv·dÌ mÏ to do rozpak˘, ale musÌm tÏ poû·dat jeötÏ o jednu laskavost.
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_06_05"); //Jelikoû v dohlednÈ dobÏ nem˘ûu Lehmarovi sv˘j dluh splatit, chyst· se na mÏ poslat ty svÈ gorily.
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_06_06"); //Dej mi jeötÏ dalöÌch 100 zlat˝ch a j· tÏ zaËnu uËit.

		Info_ClearChoices (DIA_Thorben_PleaseTeach);
		Info_AddChoice (DIA_Thorben_PleaseTeach, "Kolik?", DIA_Thorben_PleaseTeach_Later);
		Info_AddChoice (DIA_Thorben_PleaseTeach, "Dobr·. Tady je 100 zlat˝ch.", DIA_Thorben_PleaseTeach_Pay100);
	}
	else if (MIS_Matteo_Gold == LOG_SUCCESS) //Grittas Schulden bezahlt
	{
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_06_07"); //Zaplatil jsi Grittin dluh u Mattea. Vypad·ö jako spr·vn˝ chlap. NauËÌm tÏ, co budeö chtÌt.
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_06_08"); //NicmÈnÏ nem˘ûu to dÏlat zadarmo. JeötÏ po¯·d m·m f˘ru dluh˘ a pot¯ebuju penÌze.
		AI_Output (other, self,"DIA_Thorben_PleaseTeach_15_09"); //Kolik?
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_06_10"); //200 zlat˝ch.
		
		Info_ClearChoices (DIA_Thorben_PleaseTeach);
		Info_AddChoice (DIA_Thorben_PleaseTeach, "Kolik?", DIA_Thorben_PleaseTeach_Later);
		Info_AddChoice (DIA_Thorben_PleaseTeach, "Dobr·. Tady je 200 zlat˝ch.", DIA_Thorben_PleaseTeach_Pay200);
	}
	else
	{
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_06_11"); //Hmm... nevÌm, jestli se ti d· vÏ¯it, nebo ne.
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_06_12"); //Podle toho, co vÌm, m˘ûeö klidnÏ b˝t jednÌm z tÏch darmoölap˘, co p¯iöli do mÏsta jen proto, aby vypr·zdnili truhlice bohat˝ch lidÌ.
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_06_13"); //Nebudu tÏ uËit nic, dokud se nep¯esvÏdËÌm, ûe jsi dobr˝ ËlovÏk.
	};
};

func void DIA_Thorben_PleaseTeach_Pay200()
{
	AI_Output (other, self,"DIA_Thorben_PleaseTeach_Pay200_15_00"); //Dobr·. Tady je 200 zlat˝ch.
	
	if (B_GiveInvItems (other, self, ItMi_Gold, 200))
	{
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_Pay200_06_01"); //Tyhle penÌze by mi mÏly pomoct. M˘ûeme zaËÌt hned, jak budeö p¯ipraven.
		Thorben_TeachPlayer = TRUE;
	}
	else
	{
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_Pay200_06_02"); //JeötÏ po¯·d ti do 200 zlat˝ch p·r mincÌ chybÌ. Ty penÌze pot¯ebuju.
	};
	
	Info_ClearChoices (DIA_Thorben_PleaseTeach);
};

func void DIA_Thorben_PleaseTeach_Pay100()
{	
	AI_Output (other, self,"DIA_Thorben_PleaseTeach_Pay100_15_00"); //Dobr·. Tady je 100 zlat˝ch.
		
	if (B_GiveInvItems (other, self, ItMi_Gold, 100))
	{
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_Pay100_06_01"); //V tom p¯ÌpadÏ m˘ûeme zaËÌt hned, jak budeö p¯ipraven.
		Thorben_TeachPlayer = TRUE;
	}
	else
	{
		AI_Output (self, other,"DIA_Thorben_PleaseTeach_Pay100_06_02"); //Hej, jeötÏ po¯·d ti do 100 zlat˝ch chybÌ p·r mincÌ.
	};
	
	Info_ClearChoices (DIA_Thorben_PleaseTeach);
};
	
func void DIA_Thorben_PleaseTeach_Later()
{	
	AI_Output (other, self,"DIA_Thorben_PleaseTeach_Later_15_00"); //Moûn· pozdÏji.

	Info_ClearChoices (DIA_Thorben_PleaseTeach);
};

// ************************************************************
// 		Schlˆsser knacken lernen		//E4
// ************************************************************
INSTANCE DIA_Thorben_Teach(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 2;
	condition	= DIA_Thorben_Teach_Condition;
	information	= DIA_Thorben_Teach_Info;
	permanent	= TRUE;
	description = B_BuildLearnString("NauË mÏ p·Ëit z·mky!", B_GetLearnCostTalent(other, NPC_TALENT_PICKLOCK, 1));
};                       
FUNC INT DIA_Thorben_Teach_Condition()
{	
	if (Thorben_TeachPlayer == TRUE)
	&& (Npc_GetTalentSkill (other, NPC_TALENT_PICKLOCK) == 0)
	{
		return TRUE;
	};
};
FUNC VOID DIA_Thorben_Teach_Info()
{	
	AI_Output (other, self,"DIA_Thorben_Teach_15_00"); //NauË mÏ p·Ëit z·mky!
	
	if B_TeachThiefTalent (self, other, NPC_TALENT_PICKLOCK)
	{
		AI_Output (self, other,"DIA_Thorben_Teach_06_01"); //Pot¯ebujeö akor·t nÏjak˝ paklÌË. Pokud jÌm budeö v z·mku opatrnÏ ot·Ëet doprava a doleva, m˘ûeö mechanismus otev¯Ìt.
		AI_Output (self, other,"DIA_Thorben_Teach_06_02"); //Ale pokud otoËÌö p¯Ìliö rychle nebo moc silnÏ ve öpatnÈm smÏru, paklÌË se zlomÌ.
		AI_Output (self, other,"DIA_Thorben_Teach_06_03"); //»Ìm budeö obratnÏjöÌ, tÌm mÈnÏ budeö pot¯ebovat paklÌË˘. To je vöechno, opravdu.
	};
};

// ************************************************************
// 		TRADE (PERM)
// ************************************************************
INSTANCE DIA_Thorben_TRADE(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 3;
	condition	= DIA_Thorben_TRADE_Condition;
	information	= DIA_Thorben_TRADE_Info;
	permanent	= TRUE;
	description = "M˘ûeö mi prodat nÏjakÈ paklÌËe?";
	trade		= TRUE;
};                       
FUNC INT DIA_Thorben_TRADE_Condition()
{	
	if (Npc_KnowsInfo (other, DIA_Thorben_Locksmith))
	{
		return TRUE;
	};
};
FUNC VOID DIA_Thorben_TRADE_Info()
{	
	AI_Output (other, self,"DIA_Thorben_TRADE_15_00"); //M˘ûeö mi prodat nÏjakÈ paklÌËe?
	
	if (Npc_GetTalentSkill (other, NPC_TALENT_PICKLOCK) > 0)
	{
		AI_Output (self, other,"DIA_Thorben_TRADE_06_01"); //Pokud mi jeötÏ nÏjakÈ zbyly...
	}
	else
	{
		AI_Output (self, other,"DIA_Thorben_TRADE_06_02"); //No tak dobr·. Ale dokud nevÌö, jak s nimi zach·zet, tak ti budou k niËemu.
	};
	
	if (Npc_HasItems (self, ITke_Lockpick) == 0) 
	&& (Kapitel > Dietrichgeben) 
	{
		CreateInvItems (self, ITKE_LOCKPICK,5);
		Dietrichgeben = Dietrichgeben +1;
	};
	Log_CreateTopic (Topic_CityTrader,LOG_NOTE);
	B_LogEntry (Topic_CityTrader,"Tesa¯ Thorben prod·v· paklÌËe."); 
};

///////////////////////////////////////////////////////////////////////
//	Info MissingPeople
///////////////////////////////////////////////////////////////////////
instance DIA_Addon_Thorben_MissingPeople		(C_INFO)
{
	npc		 = 	VLK_462_Thorben;
	nr		 = 	4;
	condition	 = 	DIA_Addon_Thorben_MissingPeople_Condition;
	information	 = 	DIA_Addon_Thorben_MissingPeople_Info;

	description	 = 	"Ty uû jsi nÏkdy nÏjakÈho uËednÌka mÏl?";
};

func int DIA_Addon_Thorben_MissingPeople_Condition ()
{
	if (Npc_KnowsInfo (other, DIA_Thorben_Arbeit))
	&& (SC_HearedAboutMissingPeople == TRUE)
	&& (Elvrich_GoesBack2Thorben == FALSE)
		{
			return TRUE;
		};
};

func void DIA_Addon_Thorben_MissingPeople_Info ()
{
	AI_Output	(other, self, "DIA_Addon_Thorben_MissingPeople_15_00"); //Uû jsi nÏkdy mÏl nÏjakÈho uËednÌka?
	AI_Output	(self, other, "DIA_Addon_Thorben_MissingPeople_06_01"); //Jo, a ani to nenÌ tak d·vno.
	AI_Output	(other, self, "DIA_Addon_Thorben_MissingPeople_15_02"); //A co se stalo?
	AI_Output	(self, other, "DIA_Addon_Thorben_MissingPeople_06_03"); //Jmenoval se Elvrich. Byl to m˘j synovec.
	AI_Output	(self, other, "DIA_Addon_Thorben_MissingPeople_06_04"); //A byl jsem s nÌm docela spokojen˝, ale jednoho dne prostÏ nep¯iöel do pr·ce.
	
	MIS_Thorben_BringElvrichBack = LOG_RUNNING;

	Log_CreateTopic (TOPIC_Addon_MissingPeople, LOG_MISSION);
	Log_SetTopicStatus(TOPIC_Addon_MissingPeople, LOG_RUNNING);
	B_LogEntry (TOPIC_Addon_MissingPeople,"Elvrich, uËednÌk tesa¯e Thorbena, zmizel."); 
	
	Info_ClearChoices	(DIA_Addon_Thorben_MissingPeople);
	Info_AddChoice	(DIA_Addon_Thorben_MissingPeople, DIALOG_BACK, DIA_Addon_Thorben_MissingPeople_Back );
	Info_AddChoice	(DIA_Addon_Thorben_MissingPeople, "A ohl·sil jsi to domobranÏ?", DIA_Addon_Thorben_MissingPeople_Mil );
	Info_AddChoice	(DIA_Addon_Thorben_MissingPeople, "Jak je to dlouho, co jsi ho vidÏl naposledy?", DIA_Addon_Thorben_MissingPeople_wann );
	Info_AddChoice	(DIA_Addon_Thorben_MissingPeople, "Kde je Elvrich teÔ?", DIA_Addon_Thorben_MissingPeople_where );
};

func void DIA_Addon_Thorben_MissingPeople_Back ()
{
	Info_ClearChoices	(DIA_Addon_Thorben_MissingPeople);
};

func void DIA_Addon_Thorben_MissingPeople_wann ()
{
	AI_Output			(other, self, "DIA_Addon_Thorben_MissingPeople_wann_15_00"); //Jak je to dlouho, co jsi ho nevidÏl?
	AI_Output			(self, other, "DIA_Addon_Thorben_MissingPeople_wann_06_01"); //Asi tak Ëtrn·ct dnÌ.
};

func void DIA_Addon_Thorben_MissingPeople_where ()
{
	AI_Output			(other, self, "DIA_Addon_Thorben_MissingPeople_where_15_00"); //Kde je Elvrich teÔ?
	AI_Output			(self, other, "DIA_Addon_Thorben_MissingPeople_where_06_01"); //Jak to m·m vÏdÏt? Ale furt se poflakoval kolem toho öpinav˝ho bordelu v p¯Ìstavu.
	AI_Output			(self, other, "DIA_Addon_Thorben_MissingPeople_where_06_02"); //Ani by mÏ nep¯ekvapilo, kdyby si tam jeötÏ teÔ uûÌval s nÏjakou ötÏtkou.
};

func void DIA_Addon_Thorben_MissingPeople_Mil ()
{
	AI_Output			(other, self, "DIA_Addon_Thorben_MissingPeople_Mil_15_00"); //Ohl·sil jsi to domobranÏ?
	AI_Output			(self, other, "DIA_Addon_Thorben_MissingPeople_Mil_06_01"); //Samoz¯ejmÏ. MÏli ho chytit a dohlÌdnout, aby se ta lÌn· k˘ûe vr·tila zp·tky do pr·ce. Ale j· uû to stejnÏ zabalil.
	AI_Output			(self, other, "DIA_Addon_Thorben_MissingPeople_Mil_06_02"); //Aù si dÏl·, co chce. D¯Ìv nebo pozdÏji stejnÏ zjistÌ, ûe bez sluönÈ pr·ce si v Khorinidu neökrtne.
	AI_Output			(other, self, "DIA_Addon_Thorben_MissingPeople_Mil_15_03"); //(cynicky) Opravdu?
};

///////////////////////////////////////////////////////////////////////
//	Info ElvrichIsBack
///////////////////////////////////////////////////////////////////////
instance DIA_Addon_Thorben_ElvrichIsBack		(C_INFO)
{
	npc		 = 	VLK_462_Thorben;
	nr		 = 	5;
	condition	 = 	DIA_Addon_Thorben_ElvrichIsBack_Condition;
	information	 = 	DIA_Addon_Thorben_ElvrichIsBack_Info;

	description	 = 	"Elvrich pro tebe bude od nynÏjöka znovu pracovat.";
};

func int DIA_Addon_Thorben_ElvrichIsBack_Condition ()
{
	if (Elvrich_GoesBack2Thorben == TRUE)
	&& ((Npc_IsDead(Elvrich)) == FALSE)
	{
		return TRUE;
	};
};

func void DIA_Addon_Thorben_ElvrichIsBack_Info ()
{
	AI_Output	(other, self, "DIA_Addon_Thorben_ElvrichIsBack_15_00"); //OdteÔka pro tebe bude Elvrich znovu pracovat.
	AI_Output	(self, other, "DIA_Addon_Thorben_ElvrichIsBack_06_01"); //M˘ûu jenom doufat, ûe nezmizÌ znova, aû se kolem ochom˝tne nÏjak· suknÏ.
	AI_Output	(self, other, "DIA_Addon_Thorben_ElvrichIsBack_06_02"); //Za to, ûe jsi mi p¯ivedl uËednÌka, si vezmi tohle zlato.
	CreateInvItems (self, ItMi_Gold, 200);									
	B_GiveInvItems (self, other, ItMi_Gold, 200);
	MIS_Thorben_BringElvrichBack = LOG_SUCCESS;
	VLK_4302_Addon_Elvrich.flags = 0;
};

// ************************************************************
// 		Paladine		//E1
// ************************************************************
INSTANCE DIA_Thorben_Paladine(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 4;
	condition	= DIA_Thorben_Paladine_Condition;
	information	= DIA_Thorben_Paladine_Info;
	permanent	= FALSE;
	description = "Co vÌö o paladinech?";
};                       
FUNC INT DIA_Thorben_Paladine_Condition()
{	
	if (other.guild != GIL_PAL)
	{
		return TRUE;
	};	
};
FUNC VOID DIA_Thorben_Paladine_Info()
{	
	AI_Output (other, self,"DIA_Thorben_Paladine_15_00"); //Co vÌö o paladinech?
	AI_Output (self, other,"DIA_Thorben_Paladine_06_01"); //Nic moc. P¯ed dvÏma t˝dny sem na lodi p¯ipluli z pevniny.
	AI_Output (self, other,"DIA_Thorben_Paladine_06_02"); //Od tÈ doby jsou zav¯enÌ v hornÌ Ë·sti mÏsta.
	AI_Output (self, other,"DIA_Thorben_Paladine_06_03"); //Nikdo tady po¯·dnÏ nevÌ, proË vlastnÏ p¯ijeli.
	AI_Output (self, other,"DIA_Thorben_Paladine_06_04"); //HodnÏ lidÌ se ob·v· ˙toku sk¯et˘.
	AI_Output (self, other,"DIA_Thorben_Paladine_06_05"); //Ale j· si stejnÏ myslÌm, ûe jsou tu kv˘li rolnickÈ vzpou¯e.
};

// ************************************************************
// 		Bauernaufstand		//E2
// ************************************************************
INSTANCE DIA_Thorben_Bauernaufstand(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 4;
	condition	= DIA_Thorben_Bauernaufstand_Condition;
	information	= DIA_Thorben_Bauernaufstand_Info;
	permanent	= FALSE;
	description = "VÌö nÏco o tÈ rolnickÈ vzpou¯e?";
};                       
FUNC INT DIA_Thorben_Bauernaufstand_Condition()
{	
	if (Npc_KnowsInfo (other, DIA_Thorben_Paladine))
	&& (other.guild != GIL_SLD)
	&& (other.guild != GIL_DJG)
	{
		return TRUE;
	};
};
FUNC VOID DIA_Thorben_Bauernaufstand_Info()
{	
	AI_Output (other, self,"DIA_Thorben_Bauernaufstand_15_00"); //VÌö nÏco o tÈ rolnickÈ vzpou¯e?
	AI_Output (self, other,"DIA_Thorben_Bauernaufstand_06_01"); //PovÌd· se, ûe si velkostatk·¯ Onar najal ûoldnÈ¯e, aby ho chr·nili p¯ed kr·lovsk˝mi vojsky.
	AI_Output (self, other,"DIA_Thorben_Bauernaufstand_06_02"); //Asi uû mÏl dost toho, ûe mu paladinovÈ a domobrana po¯·d berou ˙rodu a dobytek.
	AI_Output (self, other,"DIA_Thorben_Bauernaufstand_06_03"); //JedinÈ, co jsme tady ve mÏstÏ zaznamenali, je neust·lÈ zvyöov·nÌ cen potravin.
	AI_Output (self, other,"DIA_Thorben_Bauernaufstand_06_04"); //Onarova farma leûÌ na v˝chod odsud, je to docela daleko. NevÌme, jestli se tam k nÏËemu chyst·.
	AI_Output (self, other,"DIA_Thorben_Bauernaufstand_06_05"); //Pokud se chceö dozvÏdÏt vÌc, zeptej se kupc˘ na trûiöti. MajÌ o ostrovÏ vÏtöÌ p¯ehled neû j·.
};

// ************************************************************
// 		Gritta
// ************************************************************
INSTANCE DIA_Thorben_Gritta(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 5;
	condition	= DIA_Thorben_Gritta_Condition;
	information	= DIA_Thorben_Gritta_Info;
	permanent	= FALSE;
	description = "P¯iöel jsem kv˘li GrittÏ...";
};                       
FUNC INT DIA_Thorben_Gritta_Condition()
{	
	if (MIS_Matteo_Gold == LOG_RUNNING)
	&& (!Npc_IsDead (Gritta))
	{
		return TRUE;
	};
};
FUNC VOID DIA_Thorben_Gritta_Info()
{	
	AI_Output (other, self,"DIA_Thorben_Gritta_15_00"); //P¯iöel jsem kv˘li GrittÏ.
	AI_Output (self, other,"DIA_Thorben_Gritta_06_01"); //MÈ nete¯i? O co jde? NenÌ to nic s penÏzi, ûe ne?
	AI_Output (other, self,"DIA_Thorben_Gritta_15_02"); //DluûÌ 100 zlat˝ch kupci Matteovi.
	
	AI_Output (self, other,"DIA_Thorben_Gritta_06_03"); //ÿekni, ûe to nenÌ pravda. Od tÈ doby, co se ke mnÏ ta mal· lenoöka nastÏhovala, m·m jen samÈ potÌûe!
	AI_Output (self, other,"DIA_Thorben_Gritta_06_04"); //M· dluh snad u kaûdÈho kupce ve mÏstÏ.
	AI_Output (self, other,"DIA_Thorben_Gritta_06_05"); //Musel jsem si p˘jËit 200 zlat˝ch u lichv·¯e jen proto, abych vyrovnal jejÌ dluhy. A teÔ tohle!
	if (Npc_GetDistToWP(self, "NW_CITY_MERCHANT_SHOP01_FRONT_01") < 500)
	{
		AI_Output (self, other,"DIA_Thorben_Gritta_06_06"); //Gritta by mÏla b˝t v domÏ.
	};
	AI_Output (self, other,"DIA_Thorben_Gritta_06_07"); //Jdi d·l, jen se jÌ zeptej. Ale m˘ûu ti ¯Ìct jedno: nem· ani zlatku.
	if (Npc_HasItems(Gritta, itmi_gold) >= 100)
	{
		AI_Output (other, self,"DIA_Thorben_Gritta_15_08"); //UvidÌme...
	};
};

// ************************************************************
// 		Grittas Gold genommen
// ************************************************************
INSTANCE DIA_Thorben_GrittaHatteGold(C_INFO)
{
	npc			= VLK_462_Thorben;
	nr			= 5;
	condition	= DIA_Thorben_GrittaHatteGold_Condition;
	information	= DIA_Thorben_GrittaHatteGold_Info;
	permanent	= FALSE;
	description = "Tv· nete¯ mi dala 100 zlat˝ch.";
};                       
FUNC INT DIA_Thorben_GrittaHatteGold_Condition()
{	
	if (Npc_KnowsInfo(other, DIA_Thorben_Gritta))
	&& (Npc_HasItems(Gritta, itmi_gold) < 100)
	&& (!Npc_IsDead (Gritta))
	{
		return TRUE;
	};
};
FUNC VOID DIA_Thorben_GrittaHatteGold_Info()
{	
	AI_Output (other, self,"DIA_Thorben_GrittaHatteGold_15_00"); //Tv· nete¯ mi dala 100 zlat˝ch.
	AI_Output (self, other,"DIA_Thorben_GrittaHatteGold_06_01"); //CO? Ta mal· nestydat· zmije - to bylo MOJE zlato! Vzala ho z mÈ truhly.
	AI_Output (self, other,"DIA_Thorben_GrittaHatteGold_06_02"); //Vraù mi to! MusÌm nejd¯Ìv zaplatit Lehmarovi. Matteo dostane svÈ penÌze pozdÏji!

	Info_ClearChoices (DIA_Thorben_GrittaHatteGold);
	if (MIS_Matteo_Gold == LOG_SUCCESS)
	{
		Info_AddChoice (DIA_Thorben_GrittaHatteGold, "Uû jsem dal Matteovi jeho zlato!", DIA_Thorben_GrittaHatteGold_MatteoHatEs);
	}
	else
	{
		Info_AddChoice (DIA_Thorben_GrittaHatteGold, "Ne. Ty penÌze jsou Matteovy.", DIA_Thorben_GrittaHatteGold_MatteoSollHaben);
	};

	if (Npc_HasItems(other, itmi_gold) >= 100)
	{
		Info_AddChoice (DIA_Thorben_GrittaHatteGold, "Tady je zlato.", DIA_Thorben_GrittaHatteGold_HereItIs);
	};
};

func void B_Thorben_DeletePetzCrimeGritta()
{
	if (Gritta_GoldGiven == FALSE)
	{
		AI_Output (self, other,"B_Thorben_DeletePetzCrimeGritta_06_00"); //A co vÌm o tÈ malÈ zmiji, jsem si jist˝, ûe bÏûela p¯Ìmo za mÏstskou str·ûÌ, aby tÏ taky obvinila!
		AI_Output (self, other,"B_Thorben_DeletePetzCrimeGritta_06_01"); //DohlÈdnu na to, aby se to nÏjak vy¯eöilo.
		B_DeletePetzCrime (Gritta);
	};
};

func void DIA_Thorben_GrittaHatteGold_MatteoHatEs()
{
	AI_Output (other, self,"DIA_Thorben_GrittaHatteGold_MatteoHatEs_15_00"); //Uû jsem dal Matteovi jeho zlato!
	AI_Output (self, other,"DIA_Thorben_GrittaHatteGold_MatteoHatEs_06_01"); //Sakra! Dobr· - dluh je dluh. AlespoÚ ûe sis ty penÌze nenechal. Asi bych ti za to mÏl podÏkovat.
	
	B_Thorben_DeletePetzCrimeGritta();
	
	Info_ClearChoices (DIA_Thorben_GrittaHatteGold);
};

func void DIA_Thorben_GrittaHatteGold_MatteoSollHaben()
{
	AI_Output (other, self,"DIA_Thorben_GrittaHatteGold_MatteoSollHaben_15_00"); //Ne. Ty penÌze jsou Matteovy.
	AI_Output (self, other,"DIA_Thorben_GrittaHatteGold_MatteoSollHaben_06_01"); //Takhle mÏ dostaneö do po¯·dn˝ch trabl˘. Lehmar nenÌ zrovna p¯ehnanÏ shovÌvav˝, kdyû p¯ijde ¯eË na dluhy.
	AI_Output (self, other,"DIA_Thorben_GrittaHatteGold_MatteoSollHaben_06_02"); //Ale p¯inejmenöÌm se chyst·ö splatit dluh mÈ nete¯e. Asi bych ti za to mÏl podÏkovat.
	
	B_Thorben_DeletePetzCrimeGritta();
	
	Info_ClearChoices (DIA_Thorben_GrittaHatteGold);
};

func void DIA_Thorben_GrittaHatteGold_HereItIs()
{
	AI_Output (other, self,"DIA_Thorben_GrittaHatteGold_HereItIs_15_00"); //Tady je zlato.
	B_GiveInvItems(other, self, itmi_gold, 100);
	AI_Output (self, other,"DIA_Thorben_GrittaHatteGold_HereItIs_06_01"); //DÌky! KoneËnÏ m·m alespoÚ Ë·st penÏz, co dluûÌm Lehmarovi.
	AI_Output (self, other,"DIA_Thorben_GrittaHatteGold_HereItIs_06_02"); //Nem˘ûu uvÏ¯it tomu, ûe mÏla tu drzost vzÌt mÈ zlato!
	
	B_Thorben_DeletePetzCrimeGritta();
	
	Thorben_GotGold = TRUE;
	
	Info_ClearChoices (DIA_Thorben_GrittaHatteGold);
};



















