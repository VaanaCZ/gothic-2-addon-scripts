// **********************************
// NSC benutzt Item Brush an Sweep-FP
// **********************************

func void ZS_Sweep_FP()
{	
	Perception_Set_Normal();
	
	B_ResetAll (self);
	
	AI_SetWalkmode 	(self,NPC_WALK);
		
	if (Hlp_StrCmp(Npc_GetNearestWP(self), self.wp)== 0)
    {
		AI_GotoWP	(self, self.wp);
	};

	if (Npc_HasItems (self,ItMi_Brush) == 0)
	{
		CreateInvItem (self,ItMi_Brush);
	};	
	
	
};
	
func int ZS_Sweep_FP_Loop ()
{
	if (Npc_IsOnFP (self, "SWEEP"))
	{	
		AI_AlignToFP (self);
		if (self.aivar[AIV_TAPOSITION] == NOTINPOS_WALK)
  		{
  			self.aivar[AIV_TAPOSITION] = NOTINPOS;
  		};
	}	
	else if (Wld_IsFPAvailable(self,"SWEEP"))
	{
		AI_GotoFP 		(self, "SWEEP");
		AI_StandUp 		(self);
		AI_AlignToFP 	(self);
		self.aivar[AIV_TAPOSITION] = NOTINPOS_WALK;
	}
	else
	{
		AI_AlignToWP (self);
		if (self.aivar[AIV_TAPOSITION] == NOTINPOS_WALK)
  		{
  			self.aivar[AIV_TAPOSITION] = NOTINPOS;
  		}; 
	};
	
	if (self.aivar[AIV_TAPOSITION] == NOTINPOS)
	{
		AI_UseItemToState 	(self,ItMi_Brush,1);	
		self.aivar[AIV_TAPOSITION] = ISINPOS;
	};	
	
	return LOOP_CONTINUE;
};

func void ZS_Sweep_FP_End ()
{
	AI_UseItemToState	(self, ItMi_Brush, -1);
};
